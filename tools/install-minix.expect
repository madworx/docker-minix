#!/usr/bin/expect -f

set qemu [list qemu-system-x86_64 -d cpu_reset,unimp,guest_errors -D /tmp/qemu.log -nographic -serial mon:stdio -drive format=qcow2,file=/minix.qcow2]
set timeout 300

exp_internal -f /tmp/expect.log 0

spawn {*}$qemu -boot d -cdrom /minix.iso;

expect_after {
    timeout {
        log_user 1
        puts "\n\n\n####### FAILED due to timeout."
        exit 1
    }
}

#
# Login and run installer.
#
expect "login: " { send "root\n" }

set timeout 120
expect "# " { send "setup\n" }
expect "\n:" { send "\n" }
expect "Keyboard type?" { send "\n" }
expect "Press ENTER for automatic mode" { send "\n" }
expect "Enter the disk number to use:" { send "\n" }
expect "Enter the region number to use" { send "\n" }
expect "Are you sure you want to continue?" { send "yes\n" }
expect "How big do you want your /home" { send "0\n" }
expect "Block size in kilobytes?" { send "\n" }

set timeout 3600
expect "All files will now be copied to your hard disk."
puts "\n\nExpect: Found start of 'installing files' section. Supressing output.\n\n"
log_user 0

expect "Saving random data.."
log_user 1
puts "\n\nExpect: Installer copied all files. Re-enabling output.\n\n"
set timeout 300
expect "Interface choice? " { send "\n" }
expect "Configuration choice? " { send "\n" }
expect "Menu choice? " { send "\n" }

expect "# " { send "mount /dev/c0d0p0s0 /mnt\n" }

#
# Patch the boot.cfg to use serial console.
#
expect "# " { send "sed -e 's#^\\(menu=Start.*\\)#\\1 console=tty00#' /mnt/boot.cfg > /mnt/boot.cfg.new\n" }
expect "# " { send "mv /mnt/boot.cfg.new /mnt/boot.cfg\n" }
expect "# " { send "umount /mnt\n" }

#
# Shutdown
#
expect "# " { send "poweroff -d\n" }
expect eof
